buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.hidetake:gradle-ssh-plugin:1.1.3'
    }
}

plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.8.1'
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'groovy'
apply plugin: 'osgi'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'signing'
apply plugin: 'pl.allegro.tech.build.axion-release'
apply from: 'gradle/dist.gradle'

group = 'org.mnode.ical4j'
description = '''
Support for additional non-standard iCalendar objects
'''

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
    // sonatype snapshots
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    compile 'org.mnode.ical4j:ical4j:2.+',
            'org.codehaus.groovy:groovy-all:2.4.15',
            'javax.mail:javax.mail-api:1.5.4'

    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4',
            'com.sun.mail:javax.mail:1.5.4', 'org.slf4j:slf4j-log4j12:1.7.25'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

jar {
    from(project.rootDir) {
        include "LICENSE.txt"
    }
    
    manifest {
        instruction 'Require-Capability', 'osgi.extender;filter:="(osgi.extender=osgi.serviceloader.registrar)"'
        instruction 'Provide-Capability', '''osgi.serviceloader;osgi.serviceloader="net.fortuna.ical4j.model.ParameterFactory",
            osgi.serviceloader="net.fortuna.ical4j.model.PropertyFactory",
            osgi.serviceloader="net.fortuna.ical4j.validate.CalendarValidatorFactory"
        '''
    }
}

javadoc {
    if (JavaVersion.current().isJava8Compatible()) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

scmVersion {
    tag {
        prefix = 'ical4j-extensions'
    }
    versionCreator 'versionWithBranch'
    branchVersionCreator = [
        'master': 'simple'
    ]
}

version = scmVersion.version

ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatype_username, password: sonatype_password)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: sonatype_username, password: sonatype_password)
            }

            pom.project {
                name project.name
                packaging 'jar'
                description project.description
                url 'http://ical4j.github.io'

                scm {
                    url 'https://github.com/ical4j/ical4j-extensions'
                    connection 'scm:git@github.com:ical4j/ical4j-extensions.git'
                    developerConnection 'scm:git@github.com:ical4j/ical4j-extensions.git'
                }

                licenses {
                    license {
                        name 'iCal4j - License'
                        url 'https://raw.githubusercontent.com/ical4j/ical4j/master/LICENSE'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'fortuna'
                        name 'Ben Fortuna'
                    }
                }
            }
        }
    }
}

publishing {
    publications {
        mavenArtifacts(MavenPublication) {
            from components.java
            artifact javadocJar
            artifact sourcesJar
            pom.withXml {
                asNode().appendNode('name', project.name)
                asNode().appendNode('description', project.description)
                asNode().appendNode('url', 'http://ical4j.github.io')

                def scmNode = asNode().appendNode('scm')
                scmNode.appendNode('url', 'https://github.com/ical4j/ical4j-extensions')
                scmNode.appendNode('connection', 'scm:git@github.com:ical4j/ical4j-extensions.git')
                scmNode.appendNode('developerConnection', 'scm:git@github.com:ical4j/ical4j-extensions.git')

                def licenseNode = asNode().appendNode('licenses').appendNode('license')
                licenseNode.appendNode('name', 'iCal4j - License')
                licenseNode.appendNode('url', 'https://raw.githubusercontent.com/ical4j/ical4j-extensions/master/LICENSE.txt')
                licenseNode.appendNode('distribution', 'repo')

                def developerNode = asNode().appendNode('developers').appendNode('developer')
                developerNode.appendNode('id', 'fortuna')
                developerNode.appendNode('name', 'Ben Fortuna')
            }
        }
    }
}

bintray {
    user = bintray_user
    key = bintray_key
    pkg {
        repo = 'maven'
        name = 'ical4j-extensions'
        userOrg = 'ical4j'
        licenses = ['BSD']
        vcsUrl = 'https://github.com/ical4j/ical4j-extensions.git'
        version {
            name = scmVersion.version
            desc = "iCal4j Extensions $scmVersion.version"
            released  = new Date()
            vcsTag = "ical4j-extensions-$scmVersion.version"
            gpg {
                sign = isReleaseVersion
            }
            mavenCentralSync {
                sync = isReleaseVersion
                user = sonatype_username //OSS user token: mandatory
                password = sonatype_password //OSS user password: mandatory
//                close = '0' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
    }
//    configurations = ['archives']
    publications = ['mavenArtifacts']
}
